ext {
    minSdkVersion = 15
    targetSdkVersion = 28
    compileSdkVersion = 28
    sourceCompatibilityVersion = JavaVersion.VERSION_1_8
    targetCompatibilityVersion = JavaVersion.VERSION_1_8
    daggerVersion = '2.21'
    kotlinVersion = '1.3.21'
}

ext.deps = [
        androidXAnnotation: 'androidx.annotation:annotation:1.0.2',
        appCompat: 'androidx.appcompat:appcompat:1.0.2',
        dagger: "com.google.dagger:dagger:$daggerVersion",
        daggerCompiler: "com.google.dagger:dagger-compiler:$daggerVersion",
        concrete: 'com.jaynewstrom:concrete:0.13.2',
        kotlinRuntime: "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion",
        timber: 'com.jakewharton.timber:timber:4.7.1',

        junit: 'junit:junit:4.12',
        fest: 'org.easytesting:fest-assert-core:2.0M10',
        mockitoCore: 'org.mockito:mockito-core:2.21.0',
        androidTestRunner: 'androidx.test:runner:1.1.1',
        androidTestRules: 'androidx.test:rules:1.1.1',
        androidTestJunitRunner: 'androidx.test.ext:junit:1.1.0',
        espressoCore: 'androidx.test.espresso:espresso-core:3.1.1'
]

buildscript {
    repositories {
        google()
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.3.2'
    }
}

allprojects {
    repositories {
        google()
        jcenter()
        mavenCentral()
    }

    configurations.all {
        resolutionStrategy {
            force deps.androidXAnnotation
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

subprojects {
    buildscript {
        repositories {
            jcenter()
        }
        dependencies {
            classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
            classpath 'com.github.dcendents:android-maven-gradle-plugin:1.4.1'
            classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.3.1'
        }
    }

    configureKotlinLint(it)
}

static def configureKotlinLint(Project project) {
    def closure = {
        project.configurations {
            ktlint
        }

        project.dependencies {
            ktlint 'com.github.shyiko:ktlint:0.31.0'
        }

        def ktlintCheck = project.tasks.create('ktlintCheck', JavaExec.class) {
            main = 'com.github.shyiko.ktlint.Main'
            classpath = project.configurations.ktlint
            args 'src/**/*.kt'
        }

        project.check.dependsOn ktlintCheck

        project.tasks.create('ktlintFormat', JavaExec.class) {
            main = 'com.github.shyiko.ktlint.Main'
            classpath = project.configurations.ktlint
            args '-r', 'src/**/*.kt'
        }
    }
    project.plugins.withId('kotlin-android', closure)
    project.plugins.withId('kotlin', closure)
}
