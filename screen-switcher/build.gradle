buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.4.1'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.3.1'
    }
}

apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    compileOptions {
        sourceCompatibility rootProject.ext.sourceCompatibilityVersion
        targetCompatibility rootProject.ext.targetCompatibilityVersion
    }

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName '1.0'
        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
        testApplicationId 'com.jaynewstrom.screenswitcher.androidTest'
    }
}

dependencies {
    provided deps.supportAnnotations
    testCompile deps.junit
    testCompile deps.mockitoCore
    testCompile deps.fest
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

artifacts {
    archives sourcesJar
}

group = PROJECT_GROUP_ID
version = PROJECT_VERSION

if (project.rootProject.file('local.properties').exists()) {
    Properties localProperties = new Properties()
    localProperties.load(project.rootProject.file('local.properties').newDataInputStream())

    bintray {
        user = localProperties.getProperty('bintrayUser')
        key = localProperties.getProperty('bintrayApiKey')
        configurations = ['archives']
        publish = true
        pkg {
            repo = PROJECT_REPO
            name = 'screen-switcher'
            userOrg = user
            licenses = ['Apache-2.0']
            vcsUrl = PROJECT_VCS_URL
            version {
                name = PROJECT_VERSION
                desc = PROJECT_DESCRIPTION
            }
        }
    }
}
