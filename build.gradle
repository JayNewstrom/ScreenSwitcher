ext {
    minSdkVersion = 21
    targetSdkVersion = 29
    compileSdkVersion = 29
    sourceCompatibilityVersion = JavaVersion.VERSION_1_8
    targetCompatibilityVersion = JavaVersion.VERSION_1_8
    daggerVersion = '2.28'
    kotlinVersion = '1.3.72'
    leakCanaryVersion = '2.4'
}

ext.deps = [
        androidXAnnotation: 'androidx.annotation:annotation:1.1.0',
        appCompat: 'androidx.appcompat:appcompat:1.1.0',
        dagger: "com.google.dagger:dagger:$daggerVersion",
        daggerCompiler: "com.google.dagger:dagger-compiler:$daggerVersion",
        concrete: 'com.jaynewstrom:concrete:0.14.0',
        kotlinRuntime: "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion",
        timber: 'com.jakewharton.timber:timber:4.7.1',
        rxJava: 'io.reactivex.rxjava2:rxjava:2.2.19',
        rxRelay: 'com.jakewharton.rxrelay2:rxrelay:2.1.1',
        constraintLayout: 'androidx.constraintlayout:constraintlayout:1.1.3',
        material: 'com.google.android.material:material:1.1.0',
        recyclerView: 'androidx.recyclerview:recyclerview:1.1.0',

        junit: 'junit:junit:4.13',
        fest: 'org.easytesting:fest-assert-core:2.0M10',
        mockitoCore: 'org.mockito:mockito-core:2.28.2',
        androidTestRunner: 'androidx.test:runner:1.2.0',
        androidTestRules: 'androidx.test:rules:1.2.0',
        androidTestJunitRunner: 'androidx.test.ext:junit:1.1.1',
        espressoCore: 'androidx.test.espresso:espresso-core:3.2.0',
        espressoIntent: 'androidx.test.espresso:espresso-intents:3.2.0',
        androidTestCore: 'androidx.test:core:1.2.0',
        espressoContrib: 'androidx.test.espresso:espresso-contrib:3.2.0',
        rxIdler: 'com.squareup.rx.idler:rx2-idler:0.11.0',
        leakCanaryInstrumentation: "com.squareup.leakcanary:leakcanary-android-instrumentation:${leakCanaryVersion}",
]

buildscript {
    repositories {
        google()
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.0.0'
        classpath 'com.diffplug.spotless:spotless-plugin-gradle:4.3.1'
    }
}

allprojects {
    repositories {
        google()
        jcenter()
        mavenCentral()
    }

    configurations.all {
        resolutionStrategy {
            force deps.androidXAnnotation
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

subprojects {
    buildscript {
        repositories {
            jcenter()
        }
        dependencies {
            classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
            classpath 'com.github.dcendents:android-maven-gradle-plugin:1.4.1'
            classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.3.1'
        }
    }

    apply plugin: 'com.diffplug.gradle.spotless'

    configureSpotless(it)
}

static def configureSpotless(Project project) {
    project.spotless {
        format 'misc', {
            target '**/*.gradle', '**/*.md', '**/.gitignore'

            trimTrailingWhitespace()
            indentWithSpaces()
            endWithNewline()
        }
    }

    def closure = {
        project.spotless {
            kotlin {
                target '**/*.kt'
                ktlint('0.37.0').userData([
                        'max_line_length': '120',
                        'indent_size': '4',
                        'continuation_indent_size': '4',
                        'insert_final_newline': 'true',
                        'end_of_line': 'crlf',
                ])
            }
        }
    }
    project.pluginManager.withPlugin('kotlin-android', closure)
    project.pluginManager.withPlugin('kotlin', closure)
}
